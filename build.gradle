
// For those who want the bleeding edge
buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}
repositories {
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

// Minecraft, Forge, and Java versions
sourceCompatibility = targetCompatibility = "1.8"
ext.mcversion = "1.8.9"
ext.forgeversion = "11.15.1.2318"

// Mod version
version = "3.4.1"
group = "com.google.coolalias.zeldaswordskills"
archivesBaseName = "zeldaswordskills-$mcversion"

dependencies {
    compileOnly "antique-atlas:antiqueatlas:1.8.9:4.4.0"
    compileOnly "mb-battlegear-2:1.8.9-MB_Battlegear2:Bullseye:1.0.10.2"
}

minecraft {
    version = "$mcversion-$forgeversion-$mcversion"
    runDir = "run"
    mappings = "stable_22"
}

jar {
    archiveName = archivesBaseName + "-" + version + "-universal.jar"
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    // copy everything other than mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

artifacts {
    archives deobfJar
    archives sourceJar
}
